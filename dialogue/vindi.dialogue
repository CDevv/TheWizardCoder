using Global

~ trash_bucket
Narrator: Seems to be a bucket that is used for trash.
Nolan: [#portrait=0] Really?
=> END!

~ ink_and_quill
Narrator: A table with ink and quill. This person really is a writer.
=> END!

~ writers_chair
Narrator: A chair in front of a desk.
=> END!

~ writers_bookshelf
Narrator: A bookshelf with one book - "Forest Wildlife: A Guide."
Nolan: [#portrait=0] I guess this person was really interested in nature as well..
=> END!

~ forest_painting
Narrator: A painting, resembling the forest just outside the town.
=> END!

~ forest_window
Narrator: Through the window you could see a landscape of the nearby forest, just like in the painting.
=> END!

~ fish_bucket
Narrator: A bucket full of water.. and fish.
=> END!

~ fisher_drawer
Narrator: It appears to be a drawer.
=> END!

~ sword_stand
Narrator: A sword on a stand. Try pulling it out?
- Yes
	Narrator: You decide to pull it out, but it somehow threw a InvalidOperationException
- No
	Narrator: You decide to not try to pull it out.
=> END!

~ cyanberry_basket
if GetPlayerData("GotGeraldHouseCyanberry"):
	Narrator: You already got something from here.
else:
	Narrator: A basket full of cyanberries. Get one?
	- Yes
		Narrator: You received a Cyanberry.
		do AddToInventory("Cyanberry")
		do SetPlayerData("GotGeraldHouseCyanberry", true)
	- No
		Narrator: Maybe stealing isn't a good deed.
=> END!

~ bread_bookshelf
Narrator: A bookshelf that is seemingly used for storing bread.
=> END!

~ house_oven
Narrator: A conveniently placed oven for baking bread.
=> END!

~ magic_book_table
Narrator: On top of this table is a book titled "A Guide to Arcane Arts". Only the first few pages are read.
=> END!

~ fake_staff
Narrator: This is a fake magic staff
Nolan: [#portrait=0] Not all wizards need a staff.
=> END!

~ theodore_house_glitch
if not GetPlayerData("VindiHousesPuzzle"):
	Narrator: The glitch doesn't allow you to open the door.
=> END!

~ vindi_intro_1
Timothy: [#portrait=1] So, how was the fishing?
Gregory: [#portrait=1] Eh, I got almost nothing, mate!
Gregory: [#portrait=0] Only a few fishies.
=> END!

~ vindi_intro_2
Timothy: [#portrait=1] Hm? Who is that?
=> END!

~ vindi_intro_3
if PlayerHasFollower():
	Timothy: [#portrait=0] Huh? Two travellers 'ere?
	Gertrude: [#portrait=0] Yep! We got here using a raft!
	Timothy: [#portrait=0] Oh.. I see. You two seem to be mages, yes?
	Nolan: [#portrait=1] Yeah!
	Timothy: [#portrait=0] Ah, nice. You guys might be able to help us with some issues in our town, then.
	Timothy: [#portrait=0] You can come in my house if you wanna know some details. See ya!
	Gertrude: [#portrait=2] Hold on! Are you guys cats?
	Timothy: [#portrait=0] Nope! We are so called 'wolf-people'
	Gertrude: [#portrait=0] Ah, okay.
else:
	Timothy: [#portrait=1] What ya doin' here, mate?
	Nolan: [#portrait=0] Uh.. I just got here using the raft that is on the coast outside this forest.
	Timothy: [#portrait=0] Hmm.. I see.
	Nolan: [#portrait=0] I am headed towards the Castle Town. I've heard some strange things have been happening.
	Timothy: [#portrait=0] You right! Some stuff been happenin..
	Timothy: [#portrait=2] Some places have black holes. But it's not actually a hole.
	Timothy: [#portrait=1] When you touch it, some weird text comes up. It's just weird, man..
	Timothy: [#portrait=1] Hey, are you not a mage or something?
	Nolan: [#portrait=0] Yeah.. I am!
	Timothy: [#portrait=1] Mages have, like, cool magic spells or whatever. Can you try doing something about this?
	Nolan: [#portrait=0] Yeah sure, I'll try.
	Timothy: [#portrait=0] Alright, come inside and I'll tell ya the details.
=> END!

~ vindi_intro_gertrude
Gertrude: [#portrait=3] So? Let's go help them!
Nolan: [#portrait=0] Yeah, let's see what we have to do..
=> END!

~ timothy_intro
if GetPlayerData("FishingRodSolved") and GetPlayerData("VindiTreeSolved"):
	Nolan: [#portrait=1] I fixed that issue as well.
	Timothy: [#portrait=0] Nice!
	Timothy: [#portrait=2] I can't think of anything else.. 
	Timothy: [#portrait=0] You can have a look around if you want to.
	Nolan: [#portrait=0] Ah, okay then.
	Timothy: [#portrait=0] Oh, and.. thank you!
	Nolan: [#portrait=1] No problem!
elif not GetPlayerData("FishingRodSolved") and GetPlayerData("VindiTreeSolved"):
	Nolan: [#portrait=1] I've fixed the issue with the big tree.. somehow.
	Timothy: [#portrait=1] Oh, you fixed that? What about the fishing rod?
	Nolan: [#portrait=2] Right.. I'll go fix that. 
elif GetPlayerData("FishingRodSolved") and not GetPlayerData("VindiTreeSolved"):
	Timothy: [#portrait=0] So.. did you fix it..?
	Nolan: [#portrait=0] Yep! The fishing rod now works.
	Timothy: [#portrait=0] Epic! Do you want me to tell about other issues?
	Nolan: [#portrait=0] Yeah, sure.
	Timothy: [#portrait=0] I have another friend who lives a bit to the north. 
	Timothy: [#portrait=2] There is a big tree right in front of his house' entrance.
	Timothy: [#portrait=1] Who knows how it ended up there..? 
	Timothy: [#portrait=0] Can you look into that?
	Nolan: [#portrait=0] Alright.
else:	
	if PlayerHasFollower():
		Timothy: [#portrait=0] Oh, heya! 
		Nolan: [#portrait=0] Hello! What details were you talking about?
		Timothy: [#portrait=0] Oh, right. 
		Timothy: [#portrait=0] Okay so I've noticed several spots 'round here that have strange stuff goin' on.
		Timothy: [#portrait=0] To the south of here is my friend Gregory's usual fishing spot. 
		Timothy: [#portrait=0] There are a couple of houses here and there. The issue is when you try to catch fish.
		Timothy: [#portrait=0] You always get strange black squares n stuff.. Dunno if that's nature's doing or some madlad playing around.
		Timothy: [#portrait=0] You two could look into that.
		Timothy: [#portrait=0] Also, can I know your names if that's no issue?
		Nolan: [#portrait=0] My name is Nolan!
		Gertrude: [#portrait=0] And mine is Gertrude!
		Timothy: [#portrait=0] Nice to meet you two! You can call me Timothy.
	else:
		Timothy: [#portrait=0] Oh, heya! 
		Nolan: [#portrait=0] Hello! What details were you talking about?
		Timothy: [#portrait=0] Oh, right. 
		Timothy: [#portrait=0] Okay so I've noticed several spots 'round here that have strange stuff goin' on.
		Timothy: [#portrait=0] To the south of here is my friend Gregory's usual fishing spot. 
		Timothy: [#portrait=0] There are a couple of houses here and there. The issue is when you try to catch fish.
		Timothy: [#portrait=0] You always get strange black squares n stuff.. Dunno if that's nature's doing or some madlad playing around.
		Timothy: [#portrait=0] You could look into that.
		Nolan: [#portrait=0] Hmm.. alright. Anything else?
		Timothy: [#portrait=0] You can talk to me again once you complete that one.
		Timothy: [#portrait=0] Also, may I know your name? So I know who to thank later, heh!
		Nolan: [#portrait=0] My name is Nolan. What about yours?
		Timothy: [#portrait=0] I'm Timothy!
		Nolan: [#portrait=0] Nice to meet you! My name is Nolan.
=> END!

~ gregory_point
Gregory: [#portrait=0] Oh, heya!
if GetPlayerData("HasMetGregory"):
	Gregory: [#portrait=0] So, did ya fix that fishing thing yet?
	if GetPlayerData("FishingRodSolved"):
		Nolan: [#portrait=0] Yes!
	else:
		Nolan: [#portrait=0] Nope..
else:
	if GetPlayerData("HasMetTimothy"):
		Nolan: [#portrait=0] Heya! You must be the other wolf's friend!
		Gregory: [#portrait=0] Yeah, I am. His name is Timothy, though.
		Nolan: [#portrait=0] Oh, okay.
		Gregory: [#portrait=0] And mine is Gregory.
	else:
		Nolan: [#portrait=0] Heya! You must be Timothy's friend!
		Gregory: [#portrait=0] Yeah, I am!
		Gregory: [#portrait=0] My name is Gregory. And you?
		Nolan: [#portrait=0] My name is Nolan, nice to meet ya!
		if PlayerHasFollower():
			Gertrude: [#portrait=0] And mine is Gertrude!
		
	if PlayerHasFollower():
		Gregory: [#portrait=0] So.. you two are mages, huh? We have quite strange stuff happenin' here.. 
	else:
		Gregory: [#portrait=0] So.. you a mage, huh? We have quite strange stuff happenin' here.. 
	Gregory: [#portrait=0] Other than the everyday snow, of course.
	
	if GetPlayerData("VisitedTimothyHouse"):
		if PlayerHasFollower():
			Gertrude: [#portrait=0] Timothy already told us about the fishing rod thing..
		else:
			Nolan: [#portrait=0] If you mean the weird fishing phenomenon, then Timothy already told me.
		Gregory: [#portrait=0] Oh. Alright then, good luck on that!
	else:
		Gregory: [#portrait=0] So.. I usually spend my time fishing. My usual fishing spot is to the south of here. Go right and then south. 
		Gregory: [#portrait=0] There's a an anomaly when you bring out your fishing rod and use it. After a while, you catch.. weird black squares..
		Nolan: [#portrait=0] Oh.. I see. Got it.
		
do SetPlayerData("HasMetGregory", true)
=> END!

~ brook_point
if GetPlayerData("FishingRodSolved"):
	Nolan: [#portrait=0] So.. I fixed the fishing rod!
	Brook: [#portrait=1] Really?
	Nolan: [#portrait=0] Yes! You can have it back.
	Brook: [#portrait=0] Nah mate, you have it. That's a reward for helpin' us.
else:
	Brook: [#portrait=0] 'Ello! You here to fish?
	Nolan: [#portrait=0] I guess..?
	Brook: [#portrait=2] Well uhh.. Hope you catch some fish 'cause apperently I can't!
	if GetPlayerData("HasMetGregory"):
		Nolan: [#portrait=0] Oh! Gregory already told me!
	else:
		Nolan: [#portrait=0] Uhh.. okay!
=> END!

~ gerald_point
Gerald: [#portrait=0] 'Ello, what can I do for ya today?
Nolan: [#portrait=0] Hmm..
- Who are you?
	Nolan: [#portrait=0] Who are you?
	Gerald: [#portrait=0] My name is Gerald, local merchant in Vindi.
	Gerald: [#portrait=0] Most items I sell are healing ones. If you are looking to heal, this is yer place.
	=> gerald_point
- Can I buy..
	Nolan: [#portrait=0] Can I buy something?
	Gerald: [#portrait=0] Yeah, sure!
	do OpenShop("GeraldShop")
- Nevermind.
	Nolan: [#portrait=0] Nevermind.
=> END!

~ johnson_point
Johnson: [#portrait=0] Heya, what what would you like?
- Can I buy something?
	Nolan: [#portrait=0] I wanna buy something from your shop.
	Johnson: [#portrait=0] Alright!
	do OpenShop("JohnsonShop")
- Why are you dressed in purple?
	Nolan: [#portrait=0] Why are you dressed in purple?
	Johnson: [#portrait=1] Because I'm a wizard! 
	Johnson: [#portrait=2] Or at least I wish I was.
- Nevermind.
	Nolan: [#portrait=0] Nevermind.
=> END!

~ kulber_point
if GetPlayerData("VindiTreeSolved"):
	if GetPlayerData("HasMetKulber"):
		Kulber: [#portrait=0] What's up?
	else:
		Kulber: [#portrait=0] Thanks, man!
		Kulber: [#portrait=0] Hey, you could look around my house!
	- See ya!
		Nolan: [#portrait=0] See ya!
		Kulber: [#portrait=0] C'ya!
	- Tell me about yourself
		Nolan: [#portrait=0] Uhh, could you tell me a bit about yourself?
		Kulber: [#portrait=0] Sure, what do you wanna know?
		Nolan: [#portrait=0] Hmm..
		- Why do you live on a cliffside?
			Nolan: [#portrait=0] Why do you live on this cliffside?
			Kulber: [#portrait=0] I couldn't find another place to live. Also, the view is actually quite nice.
		- What do you usually do?
			Nolan: [#portrait=0] Do you have a profession or something you usually do?
			Kulber: [#portrait=0] Actually, I do! I sometimes write in my free time.
			Kulber: [#portrait=0] Also, I am interested in nature. It's why I have a painting of the forest in my house.
		
	do SetPlayerData("HasMetKulber", true)
else:
	Kulber: [#portrait=0] A tree has been blockin' my house, man!
=> END!

~ craig_point
if GetPlayerData("HasMetCraig"):
	Craig: [#portrait=1] Have you defeated them yet?
	if GetPlayerData("HasFinishedCraigQuest"):
		Nolan: [#portrait=0] Yes!
		Craig: [#portrait=2] Nice! Thank you!
	else:
		Nolan: [#portrait=0] Nope..
else:
	Craig: [#portrait=0] Oh, hello there!
	Nolan: [#portrait=0] Heya!
	Craig: [#portrait=1] Hmm, you must be a wizard.
	Craig: [#portrait=1] Have you seen some.. monsters along the way?
	Craig: [#portrait=0] If so, please help me defeat them. They're right along the seaside. Can you do that?
	Nolan: [#portrait=0] Yeah.. sure!
	
	do SetPlayerData("HasMetCraig", true)
=> END!

~ theodore_point
if GetPlayerData("CompletedAllVindiQuests"):
	Theodore: [#portrait=0] The pile of snow is.. gone now?
else:
	Theodore: [#portrait=0] Can't walk over there unfortunately. 
	Theodore: [#portrait=0] Like, what is that large pile of snow? 
=> END!

~ sigbert_point
Sigbert: [#portrait=0] Oh, hello there!
=> END!