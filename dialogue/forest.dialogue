using Global

~ nobe_intro
Nobe: [#portrait=0] hello! would you like to buy something from my shop?
- Sure
	Nolan: [#portrait=0] Sure!
	do OpenShop("NobeShop")
- Nah, I'm good. 
	Nolan: [#portrait=0] Nah, I'm good.
	Nobe: [#portrait=0] ok! goodbye!
=> END!

~ nobe_cabin
if GetPlayerData("UnlockedNobeCabin") == false:
	if HasItemInInventory("Nobe's key"):
		Narrator: Open this cabin?
		- Yes
			Narrator: You unlocked the cabin.
			do CallRoomMethod("UnlockCabin")
		- No
			Narrator: You decide not to do anything.
	else:
		Narrator: This cabin seems to be locked.
=> END!

~ gertrude_intro_1
Gertrude: [#portrait=0] Ah!
Gertrude: [#portrait=1] It's you!
Nolan: [#portrait=0] It's me?
=> END!

~ gertrude_intro_2
Gertrude: [#portrait=1] Yep! I was told a wizard will come by!
Nolan: [#portrait=0] That's interesting..
Gertrude: [#portrait=2] Yeah..! I received a mysterious message, saying that I need to help a wizard on their journey.
Gertrude: [#portrait=3] I guess that wizard is you!
Nolan: [#portrait=0] Help me?
Gertrude: [#portrait=4] Yeah! Help you on your way to defeat, err.. 'bugs' or sorts. No idea what that means
Gertrude: [#portrait=4] But I have seen some dark creatures around the forest lately. Maybe that is what it's about.
Nolan: [#portrait=2] Maybe.
Gertrude: [#portrait=1] So yeah! I will accompany you on your journey!
Gertrude: [#portrait=2] What is your name, by the way?
Nolan: [#portrait=0] Nolan!
Gertrude: [#portrait=1] Nice to meet you, Nolan! I'm Gertrude!
=> END!

~ gertrude_wallstuff
Gertrude: [#portrait=3] Oh! It's the stuff i put my magic powder in!
Nolan: [#portrait=0] Magic what?
Gertrude: [#portrait=3] Magic powder, you silly!
=> END!

~ gertrude_couldron
Gertrude: [#portrait=1] This is the magical pot that I use to cook my soup! And magic potions, of course!
=> END!

~ gertrude_chair
Gertrude: [#portrait=0] Oh right, this colored chair..
Gertrude: [#portrait=4] I wanted stuff to look a lil' fancy, y'know? But the purple-haired dude couldn't paint it green for some reason.
Nolan: [#portrait=0] That's unfortunate..
=> END!

~ gertrude_downstairs
Gertrude: [#portrait=0] Do not touch.
=> END!

~ puzzle_intro_1
Gertrude: [#portrait=2] Wait..
Gertrude: [#portrait=2] What is this? 
=> END!

~ puzzle_intro_2
Gertrude: [#portrait=2] A black hole in the middle of a forest? And.. "EXECUTE"?
Gertrude: [#portrait=4] I haven't seen these before..
=> END!

~ puzzle_solved
Gertrude: [#portrait=3] Oh! It got solved!
=> END!

~ gertrude_puzzle
Gertrude: [#portrait=4] Do you have an idea how to get through?
=> END!

~ forest_final_1
Gertrude: [#portrait=2] Wait.. do you see that raft over there?
Gertrude: [#portrait=3] We could use it to get along the way!
Nolan: [#portrait=0] Yeah, probably!
=> END!

~ forest_final_2
Gertrude: [#portrait=3] Come on! Hop in!
=> END!